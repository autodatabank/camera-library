plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

group = 'com.github.autodatabank'
version = '1.1.0-beta3'

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        mlModelBinding true
    }

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/**']
        }
        resources {
            excludes += ['META-INF/**']
        }
    }

    // Do NOT compress tflite model files (need to call out to developers!)
    androidResources {
        noCompress 'tflite'
    }

    // predix.
    resourcePrefix 'adb_cameralibrary_'

    // namespace.
    namespace = 'kr.co.kadb.cameralibrary'
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // 종속성 충돌에 의한 Guava 필수.
    implementation 'com.google.guava:guava:31.1-jre'

    // Android Jetpack.
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.activity:activity-ktx:1.5.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.3'
    implementation 'androidx.exifinterface:exifinterface:1.3.4'
    implementation 'androidx.transition:transition-ktx:1.4.1'
    implementation "androidx.interpolator:interpolator:1.0.0"
    implementation 'androidx.databinding:databinding-compiler-common:7.3.1'

    // Android Architecture Components.
    def lifecycle_version = '2.5.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Google Material Design.
    implementation 'com.google.android.material:material:1.6.1'

    // Kotlin.
    //implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.20'

    // Kotlin Coroutines.
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // CameraX.
    def camerax_version = '1.1.0'
    implementation "androidx.camera:camera-core:$camerax_version"
    // CameraX Camera2 extensions
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:$camerax_version"

    // ML Kit.
    implementation 'com.google.mlkit:text-recognition-korean:16.0.0-beta6'

    // On Device Machine Learnings
    //implementation "com.google.android.odml:image:1.0.0-beta1"

    // Gson.
    implementation 'com.google.code.gson:gson:2.9.1'

    // Dexter.
    implementation 'com.karumi:dexter:6.2.3'

    // Timber.
    implementation 'com.jakewharton.timber:timber:5.0.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.autodatabank'
                artifactId = 'camera-library'
                version = '1.1.0-beta3'//android.defaultConfig.versionName
            }
        }
    }
}
